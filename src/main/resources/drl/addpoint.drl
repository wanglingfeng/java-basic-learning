package com.drools.study.drl

import com.drools.study.drl.domain.GradeDomain;

rule birthdayPoint
	// 过生日，则加10分，并且将当月交易比数翻倍后再计算积分
	salience 100
	lock-on-active true
	when
		$gradeDomain : GradeDomain(birthDay == true)
	then
		$gradeDomain.setGrade($gradeDomain.getGrade() + 10);
		$gradeDomain.setBuyCount($gradeDomain.getBuyCount() * 2);
		$gradeDomain.setBuyMoney($gradeDomain.getBuyMoney() * 2);
		$gradeDomain.setBillCurrentMonth($gradeDomain.getBillCurrentMonth() * 2);
		
		$gradeDomain.recordGradeLog($gradeDomain.getUserName(), "birthdayGrade");
end

rule billThisMonthPoint
	// 2011-01-08 - 2011-08-08每月信用卡还款3次以上，每满3笔赠送30分
	salience 99
	lock-on-active true
	date-effective "2016-01-08 23:59:59"
	date-expires "2019-08-08 23:59:59"
	when
		$gradeDomain : GradeDomain(billCurrentMonth >= 3)
	then
		$gradeDomain.setGrade($gradeDomain.getGrade() + $gradeDomain.getBillCurrentMonth() / 3 * 30);
		$gradeDomain.recordGradeLog($gradeDomain.getUserName(), "billCurrentMonth");
end

rule buyMoneyPoint
	// 当月购物总金额100以上，每100元赠送10分
	salience 98
	lock-on-active true
	when
		$gradeDomain : GradeDomain(buyMoney >= 100)
	then
		$gradeDomain.setGrade($gradeDomain.getGrade() + (int) $gradeDomain.getBuyMoney() / 100 * 10);
		$gradeDomain.recordGradeLog($gradeDomain.getUserName(), "buyMoneyGrade");
end

rule buyNumsPoint
	// 当月购物次数5次以上，每五次赠送50分
	salience 97
	lock-on-active true
	when
		$gradeDomain : GradeDomain(buyCount >= 5)
	then
		$gradeDomain.setGrade($gradeDomain.getGrade() + $gradeDomain.getBuyCount() / 5 * 50);
		$gradeDomain.recordGradeLog($gradeDomain.getUserName(), "buyCountGrade");
end

rule allFitPoint
	// 特别的，如果全部满足了要求，则额外奖励100分
	salience 96
	lock-on-active true
	when
		$gradeDomain:GradeDomain(buyCount >= 5 && billCurrentMonth >= 3 && buyMoney >= 100)
	then
		$gradeDomain.setGrade($gradeDomain.getGrade() + 100);
		$gradeDomain.recordGradeLog($gradeDomain.getUserName(), "allFitGrade");
end